buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'net.neoforged.moddev.legacyforge' version '2.0.91'
    id "maven-publish"
    id "dev.vfyjxf.modaccessor" version "1.2.1"
    id "eclipse"
}

base {
    archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

legacyForge {
    version = project.minecraft_version + '-' + project.forge_version

    runs {
        configureEach {
            gameDirectory = project.file('run')
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
            sourceSet = sourceSets.main

            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'debug'
            systemProperty 'forge.enabledGameTestNamespaces', 'mae2'
        }

        server {
            server()
            sourceSet = sourceSets.main

            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            programArguments.addAll('--nogui', '--world', 'world-extra')
            programArgument "-mixin.config=mixins.${mod_id}.json"
        }

        data {
            data()

            sourceSet = sourceSets.main

            programArguments.addAll('--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath())
            programArguments.addAll('--existing-mod', 'ae2')
        }
    }
    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.${mod_id}.refmap.json"
    }
}

repositories {
    mavenLocal()
    flatDir {
        dir 'libs'
    }
    mavenCentral()

    maven { url "https://maven.neoforged.net/releases/" }
    maven {
        name 'FirstDarkDev'
        url "https://maven.firstdarkdev.xyz/snapshots/"
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        url "https://maven.saps.dev/releases/"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
    maven {
        // Patchouli, JEI
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        // Curse Forge File
        url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }

    maven {// emi
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven {
        name = 'GTCEu Maven'
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
            includeGroup 'appeng'
        }
        }
    maven {
      name "GTCEu Maven"
      url "https://maven.gtceu.com/"
      content {
        includeGroup 'appeng'
        includeGroup 'com.gregtechceu.gtceu'
      }
    }
    maven {
        name 'Configuration'
        url 'https://repo.repsy.io/mvn/toma/public/'
    }
}

modAccessor {
    createTransformConfiguration(configurations.compileOnly)
    accessTransformerFiles = project.files('src/main/resources/META-INF/accesstransformer.cfg')
}

dependencies {
    accessCompileOnly("appeng:appliedenergistics2-forge:$project.ae2_version_cl")
    modImplementation("appeng:appliedenergistics2-forge:$project.ae2_version_cl")
    modImplementation("org.appliedenergistics:guideme:$project.guideme_version")

    //modRuntimeOnly("dev.toma.configuration:configuration-forge-$project.minecraft_version:$project.configuration_version")
    modImplementation("com.gregtechceu.gtceu:gtceu-$project.minecraft_version:$project.gtceu_version") { transitive = false }
    modImplementation("com.lowdragmc.ldlib:ldlib-forge-$project.minecraft_version:$project.ldlib_version") { transitive = false }

    modRuntimeOnly("mezz.jei:jei-$project.minecraft_version-forge:$project.jei_version") { transitive = false }

    modRuntimeOnly "dev.emi:emi-forge:$project.emi_version+$project.minecraft_version"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    // Mixin Extras
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:$project.mixinextras_version"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:$project.mixinextras_version"))

    compileOnly "org.jetbrains:annotations:26.0.2-1"
}

processResources {
    // set up properties for filling into metadata
    var properties = [
            "mod_license": mod_license,
            "mod_id": mod_id,
            "version": version,
            "mod_name": mod_name,
            "mod_url": mod_url,
            "mod_author": mod_author,
            "forge_version": forge_version.split("\\.")[0], // only specify major version of forge
            "minecraft_version": minecraft_version,
            "ae2_version": ae2_version,
            "gtceu_version": gtceu_version
    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_author,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : version,
                    "Implementation-Vendor"   : mod_author,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Jar).configureEach {
    destinationDirectory.set(file("$rootDir/jar"))
}
mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
    config "mixins.${mod_id}.json"
}

jar {
    manifest {
        attributes([
                "MixinConfigs": "mixins.${mod_id}.json"
        ])
    }
}